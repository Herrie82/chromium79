# Copyright 2018-2020 LG Electronics, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# SPDX-License-Identifier: Apache-2.0

import("//neva/neva.gni")

webos = {
  configs = []
  defines = []
  public_configs = []
  deps = []
  public_deps = [ "//base" ]
  _dir = get_path_info("../webos", "abspath")
  sources = [
    "$_dir/mediaplayer_ums.cc",
    "$_dir/mediaplayer_ums.h",
    "$_dir/mediaplayerneva_factory_webos.cc",
    "$_dir/webos_mediaclient.h",
    "$_dir/webos_mediaclient_stub.cc",
    "$_dir/webos_mediaclient_stub.h",
  ]

  use_umediaclient = false
  support_camera = false
  use_luna_service = false

  if (use_gst_media) {
    use_umediaclient = true
    support_camera = true
    defines += [ "UMS_INTERNAL_API_VERSION=2" ]
  }

  if (use_umediaclient) {
    sources -= [
      "$_dir/webos_mediaclient_stub.cc",
      "$_dir/webos_mediaclient_stub.h",
    ]
    sources += [
      "$_dir/media_util.cc",
      "$_dir/media_util.h",
      "$_dir/system_media_manager.cc",
      "$_dir/system_media_manager.h",
      "$_dir/umediaclient_impl.cc",
      "$_dir/umediaclient_impl.h",
    ]
  }

  if (support_camera) {
    sources += [
      "$_dir/mediaplayer_camera.cc",
      "$_dir/mediaplayer_camera.h",
    ]
  }

  if (use_luna_service) {
    deps += [ "//base/neva/webos" ]
  }
}
