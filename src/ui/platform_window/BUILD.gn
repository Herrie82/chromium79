# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//mojo/public/tools/bindings/mojom.gni")

if (use_neva_media) {
  mojom("mojom") {
    sources = [
      "neva/mojo/video_window_controller.mojom"
    ]
    public_deps = [
      "//mojo/public/mojom/base",
      "//ui/gfx/geometry/mojom",
      "//ui/gfx/mojom"
    ]
  }
}

source_set("platform_window") {
  sources = [
    "platform_window_base.cc",
    "platform_window_base.h",
    "platform_window_delegate.h",
    "platform_window_delegate_base.cc",
    "platform_window_delegate_base.h",
    "platform_window_init_properties.cc",
    "platform_window_init_properties.h",
  ]

  sources += [
    "neva/window_group_configuration.cc",
    "neva/window_group_configuration.h",
    "neva/window_group_configuration_param_traits.cc",
    "neva/window_group_configuration_param_traits.h",
  ]

  if (use_neva_media) {
    sources += [
      "neva/video_window_controller.h",
      "neva/video_window_controller_host.h",
    ]
  }

  deps = [
    "//base",
    "//ui/base",
    "//ui/gfx",
  ]

  if (use_neva_media) {
    deps += [ "//ui/platform_window:mojom" ]
  }

  if (is_fuchsia) {
    sources += [
      "fuchsia/initialize_presenter_api_view.cc",
      "fuchsia/initialize_presenter_api_view.h",
    ]

    public_deps = [
      "//third_party/fuchsia-sdk/sdk:scenic_cpp",
      "//third_party/fuchsia-sdk/sdk:sys_cpp",
      "//third_party/fuchsia-sdk/sdk:ui_policy",
      "//third_party/fuchsia-sdk/sdk:ui_views",
    ]
  }

  if (is_linux) {
    sources += [
      "platform_window_delegate_linux.cc",
      "platform_window_delegate_linux.h",
      "platform_window_linux.cc",
      "platform_window_linux.h",
    ]
  }

  if (is_win) {
    sources += [ "platform_window_win.h" ]
  }
}

group("platform_impls") {
  public_deps = [
    "//ui/platform_window/stub",
  ]
  if (use_x11) {
    public_deps += [ "//ui/platform_window/x11" ]
  } else if (is_win) {
    public_deps += [ "//ui/platform_window/win" ]
  }
}
